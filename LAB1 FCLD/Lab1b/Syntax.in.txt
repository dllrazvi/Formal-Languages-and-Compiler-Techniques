program = "STARTPROGRAM" "VARIABLES:" declarationlist ";" compoundstatement "ENDPROGRAM"

declarationlist = declaration | declaration ";" declarationlist

declaration = IDENTIFIER ":" type

type = "int" | "char" | "string" | arraydecl

arraydecl = "ARRAY" "[" INTEGER "]" "OF" type

compoundstatement = "BEGIN" statementlist "END"

statementlist = statement | statement ";" statementlist

statement = simplestatement | structurestatement

simplestatement = assignstatement | iostatement

assignstatement = IDENTIFIER "=" expression

iostatement = "READ" "(" IDENTIFIER ")" | "WRITE" "(" IDENTIFIER ")"

structurestatement = ifstatement | forstatement | whilestatement

ifstatement = "IF" condition "THEN" "BEGIN" statementlist "END" ["ELSE" "BEGIN" statementlist "END"]

forstatement = "FOR" IDENTIFIER "FROM" value "TO" value "DO" "BEGIN" statementlist "END"

whilestatement = "WHILE" condition "DO" "BEGIN" statementlist "END"

expression = term {("+" | "-") term}

term = factor {("*" | "/") factor}

factor = IDENTIFIER | constant | "(" expression ")"

constant = INTEGER | CHAR | STRING

value = IDENTIFIER | INTEGER

condition = expression RELATION expression

RELATION = "==" | "<=" | "<" | ">" | ">=" | "!="

comment = "//" {any_character} "\n" | "/*" {any_character} "*/"
